apply plugin: 'com.android.application'

def isFirebase = rootProject.file('app/google-services.json').exists()
if (isFirebase) {
    apply plugin: 'io.fabric'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        applicationId "me.liaoheng.wallpaper"
        versionCode 174
        versionName "1.7.4"
    }

    def isSigning = true
    signingConfigs {
        config {
            try {
                keyAlias KEY_ALIAS
                keyPassword KEY_PASSWORD
                storeFile file(STORE_FILE)
                storePassword STORE_PASSWORD
            } catch (Exception ignored) {
                isSigning = false
                printf("not signingConfigs")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isSigning) signingConfig signingConfigs.config
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    android.applicationVariants.all { variant ->
        if (variant.buildType.name == 'release') {
            variant.outputs.all {
                outputFileName = "${variant.name}-${variant.versionName}.apk"
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def crashlytics = "com.crashlytics.sdk.android:crashlytics:2.10.1"
def firebaseCore = "com.google.firebase:firebase-core:17.3.0"

dependencies {
    testImplementation deps.robolectric
    testImplementation deps.junit
    testImplementation 'joda-time:joda-time:2.10.3'
    //android system
    implementation deps.annotations
    implementation deps.material
    implementation deps.palette
    implementation deps.browser
    implementation deps.preference

    implementation 'androidx.work:work-runtime:2.3.4'
    implementation 'com.firebase:firebase-jobdispatcher:0.8.6'
    if (isFirebase) {
        implementation crashlytics
        implementation firebaseCore
    } else {
        printf("not exists google-services.json")
        implementation 'com.google.android.gms:play-services-base:17.2.1'
        compileOnly crashlytics
        compileOnly firebaseCore
    }

    implementation 'com.jakewharton:butterknife:10.2.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.0'

    implementation 'com.google.code.gson:gson:2.8.5'
    implementation deps.glide
    annotationProcessor deps.glide_compiler
    implementation deps.glide_okhttp3_integration
    implementation deps.okhttp
    implementation 'com.squareup.okhttp3:okhttp-dnsoverhttps:3.14.7'
    implementation deps.retrofit
    implementation deps.retrofit_converter_gson
    implementation deps.retrofit_adapter_rxjava2
    implementation deps.rxandroid2
    implementation deps.rxjava2
    implementation deps.rxlifecycle3
    implementation deps.rxlifecycle_android3
    implementation deps.joda_time_android


    //https://github.com/Gericop/DateTimePicker
    implementation ('com.takisoft.datetimepicker:datetimepicker:1.0.2'){
        exclude module: 'appcompat'
    }

    //https://github.com/scottyab/rootbeer
    implementation 'com.github.scottyab:rootbeer:0.0.8'

    //https://github.com/yshrsmz/LicenseAdapter
    implementation('net.yslibrary.licenseadapter:licenseadapter:3.0.0') {
        exclude module: 'recyclerview'
    }

    //https://github.com/Clans/FloatingActionButton
    implementation 'com.github.clans:fab:1.6.4'

    //https://github.com/grandcentrix/tray
    implementation 'net.grandcentrix.tray:tray:0.12.0'

    //https://github.com/apl-devs/AppIntro
    implementation 'com.github.AppIntro:AppIntro:5.1.0'

//    implementation group: "com.github.liaoheng", name: "common-core", version: "0.1.0-SNAPSHOT", changing: true
//    implementation group: "com.github.liaoheng", name: "common-adapter", version: "0.1.0-SNAPSHOT", changing: true
    implementation 'com.github.liaoheng:common-core:0.1.0-SNAPSHOT'
    implementation 'com.github.liaoheng:common-adapter:0.1.0-SNAPSHOT'
}
//configurations.all {
//    // check for updates every build
//    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
//}
if (isFirebase) {
    apply plugin: 'com.google.gms.google-services'
}
