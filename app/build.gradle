plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def isFirebase = rootProject.file('app/google-services.json').exists()
if (isFirebase) {
    apply plugin: 'com.google.gms.google-services'
}

android {
    namespace 'me.liaoheng.wallpaper'
    compileSdk 35

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion 35
        applicationId "me.liaoheng.wallpaper"
        versionCode 11003
        versionName "1.10.03"
    }

    def isSigning = true
    signingConfigs {
        config {
            try {
                keyAlias KEY_ALIAS
                keyPassword KEY_PASSWORD
                storeFile file(STORE_FILE)
                storePassword STORE_PASSWORD
            } catch (Exception ignored) {
                isSigning = false
                printf("not signingConfigs")
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isSigning) signingConfig signingConfigs.config
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    android.applicationVariants.configureEach { variant ->
        if (variant.buildType.name == 'release') {
            variant.outputs.configureEach {
                outputFileName = "${variant.name}-${variant.versionName}.apk"
            }
        }
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "11"
    }
    lint {
        abortOnError false
        disable 'ExtraTranslation'
    }
}

def firebaseCore = "com.google.firebase:firebase-analytics:22.1.2"

dependencies {
    testImplementation deps.robolectric
    testImplementation deps.junit
    testImplementation 'joda-time:joda-time:2.10.13'
    //android system
    implementation kdeps.core
    implementation deps.annotations
    implementation deps.material
    implementation deps.palette
    implementation deps.browser
    implementation (deps.preference){
        exclude group: 'androidx.lifecycle', module:'lifecycle-viewmodel-ktx'
    }
    implementation deps.swiperefreshlayout
    //https://stackoverflow.com/questions/75263047/duplicate-class-in-kotlin-android
//    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.10"))

    implementation 'androidx.work:work-runtime:2.10.0'
    implementation "androidx.datastore:datastore-preferences:1.1.2"
    implementation "androidx.datastore:datastore-preferences-rxjava3:1.1.2"
    implementation "androidx.datastore:datastore-preferences-proto:1.1.2"
    compileOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-rx3:1.7.3'
    if (isFirebase) {
        implementation(firebaseCore) {
            exclude module: "play-services-ads-identifier"
        }
    } else {
        printf("not exists google-services.json")
        compileOnly firebaseCore
    }

    implementation 'io.sentry:sentry-android-core:7.16.0'

    implementation deps.guava
    implementation deps.gson
    implementation deps.glide
    annotationProcessor deps.glide_compiler
    implementation deps.glide_okhttp3_integration
    implementation deps.okhttp
    implementation deps.okhttp_logging
    implementation deps.okhttp_doh
    implementation deps.retrofit
    implementation deps.retrofit_converter_gson
    implementation deps.retrofit_adapter_rxjava3
    implementation deps.rxandroid3
    implementation deps.rxjava3
    implementation deps.rxlifecycle4
    implementation deps.rxlifecycle_android4
    implementation deps.joda_time_android

    //https://github.com/Commit451/NativeStackBlur
    implementation 'com.github.Commit451:NativeStackBlur:1.0.7'

    //https://github.com/scottyab/rootbeer
    implementation 'com.github.scottyab:rootbeer:0.1.0'

    //https://github.com/Clans/FloatingActionButton
    implementation 'com.github.clans:fab:1.6.4'

    //https://github.com/apl-devs/AppIntro
    implementation 'com.github.AppIntro:AppIntro:6.3.1'

    //https://github.com/davemorrissey/subsampling-scale-image-view
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0'

    //    implementation group: "com.github.liaoheng", name: "common-core", version: "0.1.1-SNAPSHOT", changing: true
    //    implementation group: "com.github.liaoheng", name: "common-adapter", version: "0.1.1-SNAPSHOT", changing: true
    implementation 'com.github.liaoheng:common-core:0.1.2-SNAPSHOT'
    implementation 'com.github.liaoheng:common-adapter:0.1.2-SNAPSHOT'
}
//configurations.all {
//    // check for updates every build
//    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
//}